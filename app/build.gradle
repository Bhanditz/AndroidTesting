apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-allopen'

allOpen {
  // allows mocking for classes w/o directly opening them for release builds
  annotation 'vn.tiki.android.androidtesting.testing.OpenClass'
}

android {
  compileSdkVersion buildConfig.compileSdk
  defaultConfig {
    applicationId "vn.tiki.android.androidtesting"
    minSdkVersion buildConfig.minSdk
    targetSdkVersion buildConfig.targetSdk
    versionCode 1
    versionName "1.0"
    testInstrumentationRunner "vn.tiki.android.androidtesting.CustomTestRunner"
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
  testOptions {
    // required by robolectric
    unitTests {
      includeAndroidResources = true
    }
  }
  sourceSets { // Add test-common to sourceSets
    androidTest.java.srcDirs += "src/test-common/java"
    test.java.srcDirs += "src/test-common/java"
  }
  flavorDimensions "type"
  productFlavors {
    mock {
      dimension "type"
      versionNameSuffix "-mock"
      applicationIdSuffix '.mock'
    }
    real {
      dimension "type"
    }
  }
}

dependencies {

  implementation fileTree(dir: 'libs', include: ['*.jar'])
  implementation deps.kotlin.std
  implementation deps.kotlin.ktx

  implementation deps.support.design
  implementation deps.support.appcompat

  implementation deps.constraintLayout

  implementation deps.lifecycle.extensions
  implementation deps.retrofit.core

  testImplementation(deps.lifecycle.coreTesting, {
    exclude group: 'com.android.support', module: 'support-compat'
    exclude group: 'com.android.support', module: 'support-annotations'
    exclude group: 'com.android.support', module: 'support-core-utils'
  })

  testImplementation deps.test.junit
  testImplementation deps.test.truth
  testImplementation deps.test.robolectric.core
  testImplementation deps.test.robolectric.shadows
  testImplementation deps.test.mockito

  androidTestImplementation(deps.test.truth) {
    exclude group: 'com.google.guava', module: 'guava'
    exclude group: 'org.checkerframework', module: 'checker-compat-qual'
    exclude group: 'org.checkerframework', module: 'checker-qual'
  }
  androidTestImplementation deps.test.mockitoAndroid
  androidTestImplementation deps.supportTest.espressoCore
  androidTestImplementation deps.supportTest.espressoContrib
  androidTestImplementation deps.supportTest.runner
  androidTestImplementation deps.supportTest.rules
  androidTestImplementation(deps.lifecycle.coreTesting)
}
