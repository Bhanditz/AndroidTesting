apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-allopen'

allOpen {
  // allows mocking for classes w/o directly opening them for release builds
  annotation 'vn.tiki.android.androidtesting.testing.OpenClass'
}

android {
  compileSdkVersion 27
  defaultConfig {
    applicationId "vn.tiki.android.androidtesting"
    minSdkVersion 21
    targetSdkVersion 27
    versionCode 1
    versionName "1.0"
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
  testOptions {
    unitTests {
      includeAndroidResources = true
    }
  }
  sourceSets {
    androidTest.java.srcDirs += "src/test-common/java"
    test.java.srcDirs += "src/test-common/java"
  }
  flavorDimensions "type"
  productFlavors {
    mock {
      dimension "type"
      versionNameSuffix "-mock"
      applicationIdSuffix '.mock'
    }
    real {
      dimension "type"
    }
  }
}

dependencies {
  def lifecycle_version = "1.1.1"

  implementation fileTree(dir: 'libs', include: ['*.jar'])
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
  implementation 'com.android.support:appcompat-v7:27.1.1'
  implementation 'com.android.support.constraint:constraint-layout:1.1.1'
  implementation "android.arch.lifecycle:extensions:$lifecycle_version"
  implementation "androidx.core:core-ktx:0.3"

  def retrofit_version = "2.4.0"
  implementation "com.squareup.retrofit2:retrofit:$retrofit_version"

  testImplementation("android.arch.core:core-testing:$lifecycle_version", {
    exclude group: 'com.android.support', module: 'support-compat'
    exclude group: 'com.android.support', module: 'support-annotations'
    exclude group: 'com.android.support', module: 'support-core-utils'
  })
  testImplementation "junit:junit:4.12"
  testImplementation "com.google.truth:truth:0.40"
  testImplementation "org.robolectric:robolectric:3.8"
  testImplementation "org.mockito:mockito-core:2.18.3"
  androidTestImplementation "com.google.truth:truth:0.40"
  androidTestImplementation "com.android.support.test:runner:1.0.2"
  androidTestImplementation "com.android.support.test.espresso:espresso-core:3.0.2"
  androidTestImplementation("android.arch.core:core-testing:$lifecycle_version")
  implementation 'com.android.support:design:27.1.1'
}
